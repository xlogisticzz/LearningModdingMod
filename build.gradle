buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: "forge"

// define the properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

group = "com.xlogisticzz.learningModding"
version = config.mod_version + "." + config.build_number
archivesBaseName = "LearningModding"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    assetDir = "run/assets"
    modVersion = project.config.mod_version + ", " + "'build: " + project.config.build_number
    replaceIn "lib/Constants.java"
    if (project.hasProperty("signature"))
        replace "@FINGERPRINT@", project.signature
    replace "@VERSION@", modVersion
}

processResources {
    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'version.properties'

        // replaces
        expand 'version': project.config.mod_version, 'buildnumber': project.config.build_number
    }

    // copy everything else, thats we didnt do before
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'version.properties'
    }
}

// BEYOND THIS POINT.. 
// IS STUFF THATS FOR RELEASING...

// verify the properties exist.. or initialize.
if (!project.hasProperty("keystore_location")) // keystore stuff
    ext.keystore_location = "."

if (!project.hasProperty("keystore_alias")) // keystore stuff
    ext.keystore_alias = ""

if (!project.hasProperty("keystore_password")) // keystore stuff
    ext.keystore_password = ""

if (!project.hasProperty("release_loc")) // release loc
    ext.release_loc = "."
else
    release_loc = release_loc.replace('{MC}', minecraft.version).replace('{MODVER}', config.mod_version).replace('{BUILD}', config.build_number)

task signJar(dependsOn: "reobf") {
    inputs.file jar.getArchivePath()
    inputs.file keystore_location
    inputs.property "keystore_alias", keystore_alias
    inputs.property "keystore_password", keystore_password
    outputs.file jar.getArchivePath()

    // only sign if the keystore exists
    onlyIf {
        return keystore_location != "."
    }

    // the actual action.. sign the jar.
    doLast {
        ant.signjar(
                destDir: jar.destinationDir,
                jar: jar.getArchivePath(),
                keystore: keystore_location,
                alias: keystore_alias,
                storepass: keystore_password
        )
    }
}

task incrementBuildNumber() {
    doLast {
        // increment
        config.build_number = (config.build_number.toString().toInteger()) + 1

        // write back to the file
        configFile.withWriter {
            config.toProperties().store(it, "")
        }
    }
}

task release(type: Copy) {
    dependsOn "incrementBuildNumber"
    dependsOn "signJar"

    eachFile { file ->
        logger.info "copying ${file}"
    }

    // only if the release location isn't empty.
    onlyIf {
        return project.release_loc != "."
    }
}